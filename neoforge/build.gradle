plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
    common(rootProject.enabled_platforms.split(","))
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:${project.neoforge_version}"

    modImplementation "dev.architectury:architectury-neoforge:${project.architectury_api_version}"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')

    modImplementation "com.cobblemon:neoforge:${project.cobblemon_version}"
    forgeRuntimeLibrary(
            "thedarkcolour:kotlinforforge-neoforge:${project.kotlin_neo_version}"
    ) {
        exclude group: "net.neoforged.fancymodloader", module: "loader"
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/neoforge.mods.toml") {
        expand version: project.version
    }
}

sourceSets {
    main {
        resources {
            srcDir "../common/src/main/resources"
        }
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = "dev-shadow"
}

tasks.remapJar {
    dependsOn tasks.shadowJar
    archiveClassifier.set("")
    from(tasks.shadowJar)
}
